Polite Parchment Kookaburra

Medium

# Bypassing supply cap for native token (Sonic) market due to missing `mintAmount` in validation

### Summary
The [`Comptroller.mintAllowed()`](https://github.com/sherlock-audit/2024-12-mach-finance/blob/main/contracts/src/Comptroller.sol#L262-L263) fails to account for the `mintAmount` when validating the supply cap for the native token market, `cSonic`. 

This oversight allows minting operations to bypass the intended supply cap, resulting in the potential over-supply of the native token.

### Root Cause

The issue stems from the omission of the `mintAmount` addition during supply cap validation for the native asset context. Specifically, the `CSonic.getCash()` internally calls `getCashPrior()`, which calculates the cash balance as 

```solidity
address(this).balance - msg.value
```
, excluding the current `msg.value` (`mintAmount`). 

Consequently, the validation in `mintAllowed()` mistakenly assumes that the `mintAmount` is already included, leading to a miscalculation of the next total supplies.


[Comptroller.mintAllowed()](https://github.com/sherlock-audit/2024-12-mach-finance/blob/main/contracts/src/Comptroller.sol#L262-L263)
```solidity
function mintAllowed(address cToken, address minter, uint256 mintAmount) external override returns (uint256) {
--- SNIPPED ---

    uint256 supplyCap = supplyCaps[cToken];
    // Supply cap of 0 corresponds to unlimited supplying
    if (supplyCap != 0) {
        uint256 totalCash = CToken(cToken).getCash();   //@note -> getCashPrior -> underlying balance
        uint256 totalBorrows = CToken(cToken).totalBorrows();   //@note state tracking
        uint256 totalReserves = CToken(cToken).totalReserves(); //@note state tracking
        // totalSupplies = totalCash + totalBorrows - totalReserves
        uint256 totalSupplies = sub_(add_(totalCash, totalBorrows), totalReserves);

        uint256 nextTotalSupplies;

        if (_getUnderlyingAddress(CToken(cToken)) == nativeAsset) {
            // Don't add "mintAmount" since "cToken.getCashPrior()" already includes it (double counting issue)
@>          nextTotalSupplies = totalSupplies;
        } else {
            nextTotalSupplies = add_(totalSupplies, mintAmount);
        }

        require(nextTotalSupplies < supplyCap, "market supply cap reached");
    }

    // Keep the flywheel moving
    updateAndDistributeSupplierRewardsForToken(cToken, minter);
    return uint256(Error.NO_ERROR);
}
```

[CSonic.getCashPrior()](https://github.com/sherlock-audit/2024-12-mach-finance/blob/main/contracts/src/CSonic.sol#L159-L161)
```solidity
// CToken.sol
function getCash() external view override returns (uint256) {
    return getCashPrior();
}

---

// CSonic.sol
function getCashPrior() internal view override returns (uint256) {
@>    return address(this).balance - msg.value;
}
```

### Attack Path:

1. A malicious user supplies `Sonic` token into `cSonic` with an amount of the native token that exceeds the defined supply cap.
2. The `mintAllowed()` fails to correctly validate the new total supplies due to the missing addition of `mintAmount`.
3. The user successfully mints tokens beyond the supply cap.

### Impact
This issue allows users to bypass the supply cap for the native token market, potentially destabilizing the protocol by enabling the over-supply of the native token.

### Mitigation (optional)
Always include the mintAmount in the calculation of nextTotalSupplies for supply cap validation, even in the native token context.

```diff
function mintAllowed(address cToken, address minter, uint256 mintAmount) external override returns (uint256) {
// --- SNIPPED ---

    uint256 supplyCap = supplyCaps[cToken];
    // Supply cap of 0 corresponds to unlimited supplying
    if (supplyCap != 0) {
        uint256 totalCash = CToken(cToken).getCash();   //@note -> getCashPrior -> underlying balance
        uint256 totalBorrows = CToken(cToken).totalBorrows();   //@note state tracking
        uint256 totalReserves = CToken(cToken).totalReserves(); //@note state tracking
        // totalSupplies = totalCash + totalBorrows - totalReserves
        uint256 totalSupplies = sub_(add_(totalCash, totalBorrows), totalReserves);

        uint256 nextTotalSupplies;

-        if (_getUnderlyingAddress(CToken(cToken)) == nativeAsset) {
-            // Don't add "mintAmount" since "cToken.getCashPrior()" already includes it (double counting issue)
-          nextTotalSupplies = totalSupplies;
-        } else {
            nextTotalSupplies = add_(totalSupplies, mintAmount);
-        }

        require(nextTotalSupplies < supplyCap, "market supply cap reached");
    }

    // Keep the flywheel moving
    updateAndDistributeSupplierRewardsForToken(cToken, minter);
    return uint256(Error.NO_ERROR);
}
```